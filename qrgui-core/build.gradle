import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'java'
    id 'groovy'
    id "com.diffplug.spotless" version "6.3.0"
    id "io.freefair.lombok" version "8.0.1"
    id("org.springframework.boot") version "3.2.5" apply false
}

check.dependsOn spotlessCheck
gradle.startParameter.continueOnFailure = true

allprojects {

    group = 'it.cambi'

    repositories {
        mavenLocal()
        maven {
            url = 'https://repo.maven.apache.org/maven2'
        }
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile).tap {
            configureEach {
                options.compilerArgs << "-Xlint:unchecked"
                options.compilerArgs << "-Xlint:deprecation"
            }
        }
    }

}

configure(subprojects) {

    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom(SpringBootPlugin.BOM_COORDINATES)
        }
    }

    sourceCompatibility = '1.17'
    targetCompatibility = '1.17'

    dependencies {

        implementation 'org.springframework.boot:spring-boot-devtools'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

        implementation 'org.apache.commons:commons-lang3'

        runtimeOnly 'com.h2database:h2'

        testImplementation(
                'junit:junit',
                'org.apache.groovy:groovy-all:4.0.13',
                'org.spockframework:spock-core:2.4-M1-groovy-4.0',
                'org.spockframework:spock-spring:2.4-M1-groovy-4.0',
                'org.springframework.boot:spring-boot-starter-test'
        )
    }

    test {
        useJUnitPlatform()

        testLogging {
            events "started", "passed", "skipped", "failed"
            showExceptions true
            exceptionFormat "full"
            showCauses true
            showStackTraces true
        }
    }
}
